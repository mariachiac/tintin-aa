#ACTION {^The pumpkin cannon operator says, 'the record distance is %1 feet, held by %2'$}
{
	#if {&{pumpkinCannonFeet} && &{pumpkinCannonLocation}}
	{
		#if {!&{lastPumpkinCannonFeet} || $pumpkinCannonFeet >= $lastPumpkinCannonFeet}
		{
			post $name shot a pumpkin a distance of $pumpkinCannonFeet feet $pumpkinCannonLocation! The current record holder is %2 with a distance of %1 feet.;
			notify-send $name shot a pumpkin a distance of $pumpkinCannonFeet feet $pumpkinCannonLocation! The current record holder is %2 with a distance of %1 feet.;
			#math {lastPumpkinCannonFeet} {$pumpkinCannonFeet * 1}
		};
		#unvariable {pumpkinCannonFeet};
		#unvariable {pumpkinCannonLocation}
	}
}
{9}

#ACTION {^You have achieved the rank of '%1' in %2!$}
{
	post $name has achieved the rank of '%1' in %2!;
	notify-send $name has achieved the rank of '%1' in %2!;
	autobrag I have achieved the rank of '%1' in %2!
}
{5}

#ACTION {^You turn %1 years old today!$}
{
	post $name turned %1 years old today! Happy birthday!;
	notify-send $name turned %1 years old today! Happy birthday!
}
{5}

#ACTION {^You weep as you think of the %d experience that you lost.$}
{
	#if {${character[currentHp]} >= 1}
	{
		#return #nop
	};
	experience lost | grep -i you have lost | prefix silent_;
	#if {&{groupExperience}}
	{
		#if {$groupRatingIndex < 8}
		{
			#math {groupRatingIndex} {$groupRatingIndex + 1}
		};
		#math {groupExperience} {$groupExperience - %1}
	};
	#delay {2} {post $name was defeated in combat and lost %1 exp for a total loss of $lostExp experience points.};
	play death
}
{5}

#ACTION {^[event] The pumpkin cannon operator: %S shot a pumpkin a distance of %d feet %3!$}
{
	#if {"%1" == "%i$name"}
	{
		#variable {pumpkinCannonFeet} {%2};
		#variable {pumpkinCannonLocation} {%3}
	}
}
{9}

#ACTION {^ %1You are now level %2 %3, with %4 total levels!$}
{
	show stats | prefix silent_;
	show clev | prefix silent_;
	post $name just gained the official rank of level %2 #%3 and now has %4 total levels!;
	notify-send $name just gained the official rank of level %2 #%3 and now has %4 total levels!;
	autobrag %3 level!
}
{9}

#ACTION {^%w calls upon %2 to bring the corpse of $name back to life!$}
{
	post $name was resurrected by %1!;
	notify-send $name was resurrected by %1!
}
{9}

#ACTION {^You have completed the achievement:%1}
{
	post $name has completed the achievement %1!;
	notify-send $name has completed the achievement %1!;
	autobrag I have completed the achievement %1!
}
{9}

#ACTION {^You have {advanced|fallen} to position %d on the '%3' leaderboard!$}
{
	#if {"$leaderboardUpdateString" == ""}
	{
		#variable {leaderboardUpdateString} {${name} has %1 to %2 on the '%3' board}
	};
	#else
	{
		#variable {leaderboardUpdateString} {$leaderboardUpdateString\n%1 to %2 on the '%3' board}
	}
}
{9}

#ACTION {^\x28notify\x29 $name%1}
{
	post $name %1;
	notify-send $name %1
}
{9}

#ALIAS {^ginfo %S@%S %s%4$}
{
	#variable {gusername} {%1};
	#variable {gurl} {https://%2};
	#variable {gpassword} {%4};
	#showme {Now posting to GNU Social as %1@%2.};
	#history {delete}
}
{9}

#ALIAS {^leaderboard_update$}
{
	#if {"$leaderboardUpdateString" != ""}
	{
		post $leaderboardUpdateString;
		notify-send $leaderboardUpdateString;
		#variable {leaderboardUpdateString} {}
	}
}
{9}

#ALIAS {^post %1}
{
	#if {"$facebook" == "true"}
	{
		#script {dummyvar} {fbcmd post "%1" "Alter Aeon" "http://www.alteraeon.com/" &> /dev/null&}
	};
	#if {"$pump" == "true"}
	{
		#system {p post note --to followers --cc public --title "Alter Aeon - http://alteraeon.com" "%1" &> /dev/null&}
	};
	#if {"$twitter" == "true"}
	{
		#system {oysttyer -silent -norc -status="%1" &> /dev/null&}
	};
	#if {"$gnusocial" == "true"}
	{
		#if {!&{gusername} || !&{gpassword}}
		{
			#showme {Before you can post, you need to set your username and password. To do so please type ginfo username@node.ext password.};
			#break
		};
		#if {!&{gurl}}
		{
			#variable {gurl} {http://2mb.social}
		};
		#if {!&{gnuSocialMudGroup}}
		{
			#variable {gnuSocialMudGroup} {muds}
		};
		#system {curl -s -u "$gusername:$gpassword" --data-binary status="$(echo -e "%1") #AlterAeon !$gnuSocialMudGroup" -d source="tintin-alteraeon" $gurl/api/statuses/update.xml &> /dev/null&}
	}
}
{9}

#ALIAS {^{facebook|gnusocial|pump.io|twitter|nofacebook|nognusocial|nopump.io|notwitter}$}
{
	#switch {"%0"}
	{
		#case {"facebook"}
		{
			#variable {facebook} {true};
			#showme {Achievements will now be posted to Facebook.}
		};
		#case {"nofacebook"}
		{
			#unvariable {facebook};
			#showme {Achievements will no longer be posted to Facebook.}
		};
		#case {"gnusocial"}
		{
			#variable {gnusocial} {true};
			#showme {Achievements will now be posted to GNU Social. Remember to set your information with ginfo webfinger password.}
		};
		#case {"nognusocial"}
		{
			#unvariable {gnusocial};
			#showme {Achievements will no longer be posted to GNU Social.}
		};
		#case {"pump.io"}
		{
			#variable {pump} {true};
			#showme {Achievements will now be posted to Pump.io.}
		};
		#case {"nopump.io"}
		{
			#unvariable {pump};
			#showme {Achievements will no longer be posted to Pump.io.}
		};
		#case {"twitter"}
		{
			#variable {twitter} {true};
			#showme {Achievements will now be posted to Twitter.}
		};
		#case {"notwitter"}
		{
			#unvariable {twitter};
			#showme {Achievements will no longer be posted to Twitter.}
		}
	}
}
{9}

#VARIABLE         {leaderboardUpdateString}     {}
