#ACTION {%1 begins trying to pick %2...$}
{
	#if {"%1" == "${group[leader]}"}
	{
		#regexp {%2} {{doors|door|gates|gate|portcullis|trapdoor}} {pick &1};
		#else
		{
			#regexp {%2} {{.*} {.*}} {pick &2};
			#else
			{
				pick %2
			}
		}
	}
}
{5}

#ACTION {^Yo{u|ur} {lock picking skills failed you during that try|try another go at picking the lock|continue to manipulate the lock|attempt to pick the lock|thief abilities were insufficient for that attempt}{\.|\.\.\.}$}
{
	#if {"${character[cspam]}" == "true"}
	{
		#line {gag}
	}
}
{5}

#ACTION {^{stealing_You|You} do your best to explain to %2 that {it was an honest mistake|you didn't do it}.$}
{
	#math {i} {1d3};
	#switch {$i}
	{
		#case {1}
		{
			#variable {channel[message]} {honest! I was only trying to tickle your balls!}
		};
		#case {2}
		{
			#variable {channel[message]} {Sorry, I had a terrible itch and must have gotten the wrong pocket!}
		};
		#default
		{
			#variable {channel[message]} {I just wanted to scratch and sniff! Is that really so bad?}
		}
	};
	#regexp {%2} {{.*} {.*}} {talk &2 ${channel[message]}};
	#else
	{
		talk %2 ${channel[message]}
	}
}
{5}

#ACTION {^You slip {from the shadows|quietly from your hiding spot}.$}
{
	#variable {busy} {false};
	play hide-slip
}
{8}

#ACTION {^You throw a %1 {bottle|potion} of %3 at %4, but it fails to break!$}
{
	get %2
}
{8}

#ACTION {^You are forced to defend yourself against %2!$}
{
	#if {"${auto[withdraw]}" == "true"}
	{
		withdraw
	}
}
{9}

#ACTION {^You attempt to pin %1's shadow to the ground, but miss!$}
{
	#if {"${skillsAndSpells[shadow bind]}" == "true" && "${character[inBattle]}" == "false" && &{client[bindName]}}
	{
		shadow bind ${client[bindName]}
	}
}
{9}

#ACTION {^You slip%*hiding spot.$}
{
	#variable {busy} {true};
	play hide-success
}
{9}

#ACTION {^You throw %1 at %2{!|\.}$}
{
	#regexp {%1} {{.*} {.*}} {#if {"&2" != "{electricity|fire.*|ice|snowball|unfire}"} {get &2}}
}
{9}

#ACTION {^You throw caution to the wind and engage %1 in melee combat!$}
{
	#if {"${auto[stop]}" == "true"}
	{
		stop
	}
}
{9}

#ACTION {^You try to move stealthily, but fail!$}
{
	#variable {busy} {false};
	play hide-fail
}
{9}

#ACTION {^Your reflexes trigger and you instantly wake to battle!$}
{
	#if {&{position}}
	{
		#unvariable {position}
	};
	#else
	{
		#if {"${skillsAndSpells[withdraw]}" == "true"}
		{
			withdraw
		}
	}
}
{9}

#ACTION {^give crystal %1You don't see anyone named '%2' here.$}
{
	${auto[junkType]} crystal
}
{9}

#ACTION {^give crystal %1You fail to cast the spel%*.$}
{
	vcast crystal light | prefix give crystal %1
}
{9}

#ACTION {^give crystal %1You give a brightly glowing crystal to %2.$}
{
	bind %1
}
{9}

#ACTION {^give crystal %1You hold your hands a few inches apart and grow a glowing crystal between them.$}
{
	give crystal %1 | prefix give crystal %1
}
{9}

#ACTION {^silent_You {carefully, carefully|carefully|disconnect|drain|scoop|scrape|unmount}%*.}
{
	#showme {You %1%2.};
	#if {"${auto[junk]}" == "true"}
	{
		#if {"%1" == "carefully, carefully"}
		{
			${auto[junkType]} lightning
		};
		#elseif {"%1" == "disconnect"}
		{
			${auto[junkType]} voltaic
		};
		#elseif {"%1" == "drain"}
		{
			${auto[junkType]} vial
		};
		#else
		{
			#regexp {%2} {{.*} {.*}} {${auto[junkType]} &2}
		}
	}
}
{9}

#ACTION {^stealing_%1 seems to have dropped %2!%sYou'll surely return it later.$}
{
	play pickpocket;
	#if {"${auto[junk]}" == "true"}
	{
		#regexp {%2} {{.*} {.*}} {#if {"&2" == "envelope"} {${auto[junkType]} donation};#elseif {"&2" == "'2"} {${auto[junkType]} paper};#else {${auto[junkType]} &2}};
		#else
		{
			${auto[junkType]} %2
		}
	}
}
{9}

#ALIAS {{swi|swie|swiel|swield} %1}
{
	#variable {stabWeapon} {%1};
	#showme {You will now use the %1 for backstabs.}
}
{5}

#ALIAS {^get %S %S$}
{
	#if {"${skillsAndSpells[palm]}" == "true" && "$inBattle" != "true" && "%1" != "all" && "$position" != "sleep"}
	{
		palm %1 %2
	};
	#else
	{
		#send {get %1 %2}
	}
}
{8}

#ALIAS {^{bin|bind} %1}
{
	shadow bind %1;
	#variable {client[bindName]} {%1}
}
{9}

#ALIAS {^{bin|bind}$}
{
	shadow bind;
	#variable {client[bindName]} {}
}
{9}

#ALIAS {^{strik|strike}%*}
{
	#regexp {%0} {{.*} {.*}} {shadow strike &2} {shadow strike}
}
{9}

#ALIAS {^{lb|lbi|lbin|lbind|lightb|lightbi|lightbin|lightbind} %1}
{
	#if {"${skillsAndSpells[crystal light]}" == "true"}
	{
		vcast crystal light | prefix give crystal %1
	};
	#else
	{
		#echo {You need to learn the spell crystal light to use lightbind.}
	}
}
{9}

#ALIAS {^{noauto|auto}{stop|withdraw}$}
{
	#switch {"%0"}
	{
		#case {"autowithdraw"}
		{
			#variable {auto[withdraw]} {true};
			#showme {autowithdraw enabled.}
		};
		#case {"autostop"}
		{
			#variable {auto[stop]} {true};
			#showme {autostop enabled.}
		};
		#default
		{
			#unvariable {auto[%2]};
			#showme {auto%2 disabled.}
		}
	}
}
{9}

#ALIAS {^{pickp|pickpo|pickpoc|pickpock|pickpocke|pickpocket} %s%3}
{
	#send {pickpocket %3 | prefix stealing_}
}
{9}

#ALIAS {bs %1}
{
	#variable {victim} {%1};
	#if {&{weapon}}
	{
		remove $weapon
	};
	#if {&{eqshield}}
	{
		remove shield
	};
	wield $stabWeapon;
	#if {"$cloakOfDarkness" == "true"}
	{
		cloak on
	};
	#if {"$hide" == "true"}
	{
		hide
	};
	backstab %1;
	remove $stabWeapon;
	#if {&{weapon}}
	{
		wield $weapon
	};
	#if {&{eqshield}}
	{
		wear shield
	};
	#if {"$cloakOfDarkness" == "true"}
	{
		cloak off
	}
}
{9}

#ALIAS {slay %1 %2}
{
	#variable {victim} {%1};
	#if {&{weapon}}
	{
		remove $weapon
	};
	#if {&{eqshield}}
	{
		remove shield
	};
	wield $stabWeapon;
	#if {"$cloakOfDarkness" == "true"}
	{
		cloak on
	};
	#if {&{stealthyMovement}}
	{
		sneak %2
	};
	#else
	{
		%2
	};
	backstab %1;
	remove $stabWeapon;
	#if {&{eqshield}}
	{
		wear shield
	};
	#if {&{weapon}}
	{
		wield $weapon
	};
	#if {"$cloakOfDarkness" == "true"}
	{
		cloak off
	}
}
{9}

#SUBSTITUTE       {^stealing_}  {}  {1}
#SUBSTITUTE       {^give crystal %1You}  {You}  {9}
