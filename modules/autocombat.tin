#ACTION {^You jump to {assist|attack} %*!$}
{
	#undelay {autocombatDelay};
	#unvariable {autocombatPaused};
	#delay {autocombatDelay} {#var {autocombatPaused} {1}} {60}
}
{9}

#ACTION {^You're already targeting %*!$}
{
	#undelay {autocombatDelay};
	#unvariable {autocombatPaused};
	#delay {autocombatDelay} {#var {autocombatPaused} {1}} {60}
}
{9}

#ACTION {^{If you really want to cast that on yourself, you must use your name\.|They are no longer here\.|You don't have enough mana\.|\w+ who\?|\w+ whom\?|You are not currently in battle\.  What enemy do you want to cast it on\?}$}
{
	#variable {autocombatPaused} {1};
	#if {"%0" == "You don't have enough mana."}
	{
		synth_play warningSound
	}
}
{9}

#ALIAS {^autocombat %1 %d$}
{
	#list {autocombatList} {clear};
	#list {autocombatList} {add} {@strip_braces{%1}};
	autocombat;
	#echo {Will execute every %2 seconds.};
	@createAutocombatTicker{%2};
	qcsave
}
{8}

#ALIAS {^autocombat {pause|resume}$}
{
	#if {"%1" == "pause"}
	{
		#variable {autocombatForcePaused} {1};
		#showme {Autocombat paused.}
	};
	#else
	{
		#unvariable {autocombatForcePaused};
		#showme {Autocombat resumed.}
	}
}
{8}

#ALIAS {^autocombat %1}
{
	#list {autocombatList} {clear};
	#list {autocombatList} {add} {@strip_braces{%1}};
	autocombat;
	#echo {Will execute every 3 seconds.};
	@createAutocombatTicker{3};
	qcsave
}
{9}

#ALIAS {^autocombat$}
{
	#local {i} {};
	#echo {Automatic combat:};
	#if {"${autocombatList}" != ""}
	{
		#foreach {${autocombatList[]}} {i}
		{
			#echo {${autocombatList[$i]}}
		}
	};
	#echo {To set your autocombat commands, type autocombat followed by a list of commands enclosed in braces and separated by semicolons.}
}
{9}

#ALIAS {^noautocombat$}
{
	#list {autocombatList} {clear};
	#echo {automatic combat disabled.};
	#unticker {auto_combat};
	qcsave
}
{9}

#FUNCTION {createAutocombatTicker}
{
	#ticker {auto_combat}  {#if {"${autocombatList}" != "" && !&{autocombatForcePaused} && !&{autocombatPaused}}{#var {autocombatAttack} {${autocombatList[@list_rand{autocombatList}]}};#if {"$autocombatAttack" != "{c|ca|cas|cast|vc|vca|vcas|vcast} %*" && "${kxwt[fighting]}" == "-1"} {#var {autocombatPaused} {1};#unvar {autocombatAttack}};#else {$autocombatAttack;#unvar {autocombatAttack}}}}                                                                                            {%1};
	#return #nop
}

