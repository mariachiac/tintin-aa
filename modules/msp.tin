#ACTION {^!!SOUND(%1.wav{\)| R=\d*\)}$}
{
	#nop {Keep track of the last 10 items sent via msp.};
	#list {soundHistory} {insert} {1} {%1};
	#list {soundHistory} {size} {i};
	#if {$i >= 11}
	{
		#list {soundHistory} {delete} {11}
	};
	#system {if [ ! -f "$HOME/sounds/%1.wav" ] ; then DIR=$(echo "%1.wav" | sed "s/\(.*\\/\)[^\\/]*$/\\1/");FNAME=$(echo "%1.wav" | sed -e "s/.*\\/\([^\\/]*\)$/\\1/");mkdir -p "$HOME/sounds/$DIR";sox -V0 "http://alteraeon.com/soundpack/wav_v1/%1.wav" -r 22050 "$HOME/sounds/$DIR/$FNAME" 2> /dev/null;fi};
	#switch {"%1"}
	{
		#case {"skill/victory_cry_%*"}
		{
			seq_play %1
		};
		#case {"spell/bolt"}
		{
			seq_play spell/bolt
		};
		#case {"spell/confusion"}
		{
			seq_play spell/confusion
		};
		#case {"misc/{godnotify|notify}"}
		{
			seq_play %1
		};
		#case {"weather/{rainstart|rainstop|rain1|rain2|rain3|rain4|rain5}"}
		{
			seq_play %1
		};
		#default
		{
			#if {"%1" != "Off U=http://www.alteraeon.com/soundpack/wav_v1/"}
			{
				play %1
			}
		}
	}
}
{9}

#ACTION {^{silent_MSP|MSP} mode is {already|now} {dis|en}abled.$}
{
	#nop {Because one action handles this, we need to check for en or dis. En is enabled, dis is disabled.};
	#if {"%3" == "en"}
	{
		#variable {msp} {true}
	};
	#else
	{
		#variable {msp} {false}
	}
}
{9}

#ALIAS {^synth_play %1}
{
	#if {!&{volume}}
	{
		#variable {volume} {0}
	};
	#if {"$msp" == "true" && &{%1}}
	{
		#system {play -qV0 $%1 gain $volume &> /dev/null&}
	}
}
{9}

#ALIAS {seq_{play|play_norandom} %S}
{
	#if {!&{volume}}
	{
		#variable {volume} {0}
	};
	#if {!&{play_%1} && "$msp" == "true"}
	{
		#variable {play_%1} {false};
		#if {"%1" == "play"}
		{
			#script {soundFileName} {$shuf -n 1 -e $HOME/sounds/%2*wav};
			#variable {soundFileName} {${soundFileName[1]}}
		};
		#else
		{
			#variable {soundFileNamei} {$HOME/sounds/%2.wav}
		};
		#system {play -qV0 "$soundFileName" gain $volume &> /dev/null &};
		#script {i} {soxi "$soundFileName" | grep Duration | cut -d ':' -f2- | cut -d '=' -f1 | tr -d "[:space:]"};
		#variable {i} {${i[1]}};
		#delay {$i}  {#unvariable {play_%1}}
	}
}
{9}

#ALIAS {play %S}
{
	#if {!&{volume}}
	{
		#variable {volume} {0}
	};
	#if {"$msp" == "true"}
	{
			#system {play -qV0 "$($shuf -n 1 -e $HOME/sounds/%1[^_]*wav)" gain $volume &> /dev/null &}
	}
}
{9}

#EVENT {IAC WILL MSP}
{
	#send {\xFF\xFD\x5A\};
	#unevent {IAC WILL MSP}
}

#GAG              {^!!SOUND%*}
#MACRO {\e>}
{
	#if {$volume <= -40}
	{
		#variable {volume} {-40}
	};
	#else
	{
		#math {volume} {$volume - 2}
	};
	synth_play newVolumeSound
}

#MACRO {\e?}
{
	#if {$volume >= 0}
	{
		#variable {volume} {0}
	};
	#else
	{
		#math {volume} {$volume + 2}
	};
	synth_play newVolumeSound
}

#VARIABLE         {msp}  {true}
#VARIABLE         {soundHistory}  {}
