#action {^You cast a powerful healing spell on yourself.$}
{
	FULL_HEAL
}
{9}

#action {^You feel sick.$}
{
	#if {"${auto[removepoison]}" == "true"}
	{
		#if {"${skillsAndSpells[cure poison]}" == "true"}
		{
			vcast cure poison
		};
		#elseif {"${skillsAndSpells[remove poison]}" == "true"}
		{
			vcast remove poison
		}
	}
}
{9}

#action {^You feel {a little better|better}{\.|!}$}
{
	FULL_HEAL
}
{9}

#alias {H}
{
	@harm{%0}
}
{9}

#alias {^/$}
{
	#if {${character[currentMana]} >= 17 && "${skillsAndSpells[word of recall]}" == "true"}
	{
		vcast word of recall
	};
	#else
	{
		recall
	}
}
{9}

#alias {^FULL_HEAL$}
{
	#if {&{character[fullHeal]}  && ${character[currentMana]} > ${character[maxMana]} / 5 && ${character[currentHp]} < ${character[maxHp]} && "${character[inBattle]}" != "true" && "$busy" != "true"}
	{
		#delay {0.6} {c ${character[fullHeal]}};
		#variable {msg} {false}
	};
	#elseif {${character[currentMana]} < ${character[maxMana]} / 5 && &{character[fullHeal]}}
	{
		#unvariable {character[fullHeal]};
		#variable {msg} {Fullheal interupted due to low mana.}
	};
	#elseif {${character[currentHp]} >= ${character[maxHp]}  && &{character[fullHeal]}}
	{
		#unvariable {character[fullHeal]};
		#variable {msg} {Fullheal complete.}
	};
	#elseif {"${character[inBattle]}" == "true" || "$busy" == "true" && &{character[fullHeal]}}
	{
		#unvariable {character[fullHeal]}
	};
	#if {&{msg} && "$msg" != "false"}
	{
		#echo {$msg};
		#variable {msg} {false}
	}
}
{9}

#alias {^fullheal$}
{
	#if {"${skillsAndSpells[major heal]}" == "true"}
	{
		#variable {character[fullHeal]} {major heal}
	};
	#elseif {"${skillsAndSpells[heal]}" == "true"}
	{
		#variable {character[fullHeal]} {heal}
	};
	#elseif {"${skillsAndSpells[bolster]}" == "true"}
	{
		#variable {character[fullHeal]} {bolster}
	};
	#else
	{
		#variable {character[fullHeal]} {soothe wounds}
	};
	vcast ${character[fullHeal]}
}
{9}

#alias {^{auto|noauto}{recall|removepoison}$}
{
	#if {"%1" == "auto"}
	{
		#variable {auto[%2]} {true};
		#echo {Auto %2 enabled.}
	};
	#else
	{
		#variable {auto[%2]} {false};
		#echo {Auto %2 disabled.}
	}
}
{9}

#alias {h}
{
	@heal{%0}
}
{9}

#function {harm}
{
	#if {"${skillsAndSpells[harm]}" == "true"}
	{
		#variable {character[harmSpell]} {harm}
	};
	#elseif {"${skillsAndSpells[inflict suffering]}" == "true"}
	{
		#variable {character[harmSpell]} {inflict suffering}
	};
	#elseif {"${skillsAndSpells[chasten]}" == "true"}
	{
		#variable {character[harmSpell]} {chasten}
	};
	#else
	{
		#variable {character[harmSpell]} {inflict wounds}
	};
	#variable {result} {c ${character[harmSpell]} %0}
}

#function {heal}
{
	#if {"%0" == "" && ${character[currentHp]} == ${character[maxHp]}}
	{
		#variable {result} {#echo {Your health is already at maximum.}}
	};
	#else
	{
		#if {"${skillsAndSpells[breath of life]}" == "true"}
		{
			#variable {character[healSpell]} {breath of life}
		};
		#elseif {"${skillsAndSpells[major heal]}" == "true"}
		{
			#variable {character[healSpell]} {major heal}
		};
		#elseif {"${skillsAndSpells[heal]}" == "true"}
		{
			#variable {character[healSpell]} {heal}
		};
		#elseif {"${skillsAndSpells[bolster]}" == "true"}
		{
			#variable {character[healSpell]} {bolster}
		};
		#else
		{
			#variable {character[healSpell]} {soothe wounds}
		};
		#variable {result} {c ${character[healSpell]} %0}
	}
}
